{
ë³¸ ì»´í¬ë„ŒíŠ¸ëŠ” BSD ë¼ì´ì„¼ìŠ¤ë¥¼ ë”°ë¥´ê³  ìˆìŠµë‹ˆë‹¤.


ì› BSD ë¼ì´ì„ ìŠ¤ì—ì„œ ë³´ì¦ ë¶€ì¸ì˜ â€œì €ì‘ê¶Œ ì†Œìœ ìì™€ ê¸°ì—¬ìâ€ êµ¬ë¬¸ì€ â€œí‰ì˜ì›â€ê³¼ ê¸°ì—¬ìë¡œâ€ í•´ì„ëœë‹¤. ë¼ì´ì„ ìŠ¤ í…œí”Œë¦¿ì€ ë‹¤ìŒê³¼ ê°™ë‹¤:

Copyright (c) 2012, Jung K. H / StyleNS / StyleNS.net
All rights reserved.

ë‹¤ìŒì˜ ì¡°ê±´ë“¤ì„ ì¶©ì¡±ì‹œí‚¤ëŠ” í•œ, ì†ŒìŠ¤ í˜•ì‹ê³¼ ë°”ì´ë„ˆë¦¬ í˜•ì‹ì„ í†µí•œ ì¬ë°°í¬ì™€ ì‚¬ìš©ì€ ìˆ˜ì • ì—¬ë¶€ì— ê´€ê³„ì—†ì´ í—ˆìš©ëœë‹¤. 

ì†ŒìŠ¤ ì½”ë“œì˜ ì¬ë°°í¬ëŠ” ìœ„ì˜ ì €ì‘ê¶Œ í‘œì‹œì™€ ì—¬ê¸° ë‚˜ì—´ëœ ì¡°ê±´ë“¤, ê·¸ë¦¬ê³  ì•„ë˜ì˜ ë³´ì¦ ë¶€ì¸ ê³ ì§€ë¥¼ í¬í•¨í•´ì•¼ í•œë‹¤. 

 

ë°”ì´ë„ˆë¦¬ í˜•ì‹ìœ¼ë¡œ ì¬ë°°í¬ í•  ë•ŒëŠ” ìœ„ì˜ ì €ì‘ê¶Œ í‘œì‹œì™€ ì—¬ê¸° ë‚˜ì—´ëœ ì¡°ê±´ë“¤ ê·¸ë¦¬ê³  ì•„ë˜ì˜ ë³´ì¦ ë¶€ì¸ ê³ ì§€ë¥¼ ë°°í¬í•  ë•Œ ì œê³µë˜ëŠ”
ë¬¸ì„œ ë° ê¸°íƒ€ ìë£Œì— í¬í•¨í•´ì•¼ í•œë‹¤. 

 

ì‚¬ì „ì— ì„œë©´ìœ¼ë¡œ í—ˆê°€ë¥¼ ë°›ì§€ ì•ŠëŠ” í•œ, StyleNSì˜ ì´ë¦„ì´ë‚˜ ê¸°ì—¬ìì˜ ì´ë¦„ì´ ë³¸ ì†Œí”„íŠ¸ì›¨ì–´ì—ì„œ ì¶”ì¶œí•œ ì œí’ˆì„ ë³´ì¦í•˜ê±°ë‚˜ í™ë³´í•˜ëŠ”ë° 
ì‚¬ìš©ë˜ì–´ì„œëŠ” ì•ˆ ëœë‹¤. 

ì €ì‘ê¶Œìì™€ ê¸°ì—¬ìëŠ” ì´ ì†Œí”„íŠ¸ì›¨ì–´ë¥¼ â€œìˆëŠ” ê·¸ëŒ€ë¡œì˜â€ ìƒíƒœë¡œ ì œê³µí•˜ë©°, ìƒí’ˆì„± ì—¬ë¶€ë‚˜ íŠ¹ì •í•œ ëª©ì ì— ëŒ€í•œ ì í•©ì„±ì— ëŒ€í•œ ë¬µì‹œì  
ë³´ì¦ì„ í¬í•¨í•œ ì–´ë– í•œ í˜•íƒœì˜ ë³´ì¦ë„ ëª…ì‹œì ì´ë‚˜ ë¬µì‹œì ìœ¼ë¡œ ì œê³µë˜ì§€ ì•ŠëŠ”ë‹¤.  ì†í•´ ê°€ëŠ¥ì„±ì„ ì‚¬ì „ì— ì•Œê³  ìˆì—ˆë‹¤ í•˜ë”ë¼ë„, ì €ì‘ê¶Œìë‚˜ 
ê¸°ì—¬ìëŠ” ì–´ë– í•œ ê²½ìš°ì—ë„ ì´ ì†Œí”„íŠ¸ì›¨ì–´ì˜ ì‚¬ìš©ìœ¼ë¡œ ì¸í•˜ì—¬ ë°œìƒí•œ, ì§ì ‘ì ì´ê±°ë‚˜ ê°„ì ‘ì ì¸ ì†í•´, ìš°ë°œì ì´ê±°ë‚˜ ê²°ê³¼ì  ì†í•´, íŠ¹ìˆ˜í•˜ê±°ë‚˜ 
ì¼ë°˜ì ì¸ ì†í•´ì— ëŒ€í•˜ì—¬, ê·¸ ë°œìƒì˜ ì›ì¸ì´ë‚˜ ì±…ì„ë¡ , ê³„ì•½ì´ë‚˜ ë¬´ê³¼ì‹¤ì±…ì„ì´ë‚˜ ë¶ˆë²•í–‰ìœ„(ê³¼ì‹¤ ë“±ì„ í¬í•¨)ì™€ ê´€ê³„ ì—†ì´ ì±…ì„ì„ ì§€ì§€ ì•ŠëŠ”ë‹¤. 
ì´ëŸ¬í•œ ì¡°ê±´ì€ ëŒ€ì²´ ì¬í™”ë‚˜ ìš©ì—­ì˜ êµ¬ì… ë° ìœ ìš©ì„±ì´ë‚˜ ë°ì´í„°, ì´ìµì˜ ì†ì‹¤, ê·¸ë¦¬ê³  ì˜ì—… ë°©í•´ ë“±ì„ í¬í•¨í•˜ë‚˜ ì´ì— êµ­í•œë˜ì§€ëŠ” ì•ŠëŠ”ë‹¤.
}

unit JkhLabel;

interface

uses
  Windows, SysUtils, Classes,
  {$if CompilerVersion <= 15}
  Controls, StdCtrls, Types, Graphics;
  {$elseif CompilerVersion > 16}
  Vcl.Controls, Vcl.StdCtrls, Vcl.Graphics, System.Types;
  {$ifend}

type
  TJkhFontWeight = (fwDONTCARE, fwTHIN, fwEXTRALIGHT, fwLIGHT, fwNORMAL,
                    fwMEDIUM, fwSEMIBOLD, fwBOLD, fwEXTRABOLD, fwHEAVY);

  TJkhLabel = class(TCustomLabel)
  private
    FFontWeightValue: Integer;
    FFontSize: Integer;
    FFontFace: String;
    FFontColor: TColor;
    FScreenLogPixels: Integer;
    FFontWeight: TJkhFontWeight;
    FFixedDPI: Integer;
    FFontStrike: Boolean;
    FAutoResizeText: Boolean;
    procedure SetFontColor(const Value: TColor);
    procedure SetFontFace(const Value: String);
    procedure SetFontSize(const Value: Integer);
    procedure SetFontWeight(const Value: TJkhFontWeight);
    procedure SetFixedDPI(const Value: Integer);
    procedure SetFontStrike(const Value: Boolean);
    procedure SetAutoResizeText(const Value: Boolean);
  protected
    procedure DoDrawText(var Rect: TRect; Flags: Longint); override;
    procedure AdjustBounds; override;
  public
    constructor Create(AOwner: TComponent); override;
  published
    property FixedDPI: Integer read FFixedDPI write SetFixedDPI default -1;
    property FontSize: Integer read FFontSize write SetFontSize;
    property FontWeight: TJkhFontWeight read FFontWeight write SetFontWeight;
    property FontFace: string read FFontFace write SetFontFace;
    property FontColor: TColor read FFontColor write SetFontColor;
    property FontStrike: Boolean read FFontStrike write SetFontStrike;

    property Align;
    property Alignment;
    property Anchors;
    property AutoSize;
    property AutoResizeText: Boolean read FAutoResizeText write SetAutoResizeText default False;

    property BiDiMode;
    property Caption;
    property Color nodefault;
    property Constraints;
    property DragCursor;
    property DragKind;
    property DragMode;
    property Enabled;
    property FocusControl;
    property Font;
    property ParentBiDiMode;
    property ParentColor;
    property ParentFont;
    property ParentShowHint;
    property PopupMenu;
    property ShowAccelChar;
    property ShowHint;
    property Transparent;
    property Layout;
    property Visible;
    property WordWrap;
    property OnClick;
    property OnContextPopup;
    property OnDblClick;
    property OnDragDrop;
    property OnDragOver;
    property OnEndDock;
    property OnEndDrag;
    property OnMouseDown;
    property OnMouseMove;
    property OnMouseUp;
    property OnMouseEnter;
    property OnMouseLeave;
    property OnStartDock;
    property OnStartDrag;
  end;

procedure Register;

implementation

procedure Register;
begin
  RegisterComponents('NS', [TJkhLabel]);
end;

{ TJkhLabel }

procedure TJkhLabel.AdjustBounds;
const
  WordWraps: array[Boolean] of Word = (0, DT_WORDBREAK);
var
  DC: HDC;
  X: Integer;
  Rect: TRect;
  AAlignment: TAlignment;
begin
  if not (csReading in ComponentState) and AutoSize then
  begin
    Rect := ClientRect;
    DC := GetDC(0);
    Canvas.Handle := DC;
    DoDrawText(Rect, (DT_EXPANDTABS or DT_CALCRECT) or WordWraps[WordWrap]);
    Canvas.Handle := 0;
    ReleaseDC(0, DC);
    X := Left;
    AAlignment := Alignment;
    if UseRightToLeftAlignment then ChangeBiDiModeAlignment(AAlignment);
    if AAlignment = taRightJustify then Inc(X, Width - Rect.Right);
    SetBounds(X, Top, Rect.Right, Rect.Bottom+2);
  end;
end;

constructor TJkhLabel.Create(AOwner: TComponent);
var
  DC: HDC;
begin
  inherited;

  If FixedDPI <= 0 Then
  Begin
     DC := GetDC(0);
     FScreenLogPixels := GetDeviceCaps(DC, LOGPIXELSY);
     ReleaseDC(0,DC);
  End
  Else
     FScreenLogPixels := FixedDPI;

  FFontWeight := fwNORMAL;
  FFontSize := 8;
  FFontFace := 'Gulim';
  FFontColor := clBlack;
  FFontStrike := False;
end;

procedure TJkhLabel.DoDrawText(var Rect: TRect; Flags: Integer);
var
  Text: string;
  lf: LOGFONT;

  AutoResizeLoop: Integer;
  ResizeCandidate: Integer;
  TextBaseSize: TSize;
begin
  Text := GetLabelText;
  if (Flags and DT_CALCRECT <> 0) and ((Text = '') or ShowAccelChar and
    (Text[1] = '&') and (Text[2] = #0)) then Text := Text + ' ';
  if not ShowAccelChar then Flags := Flags or DT_NOPREFIX;
  Flags := DrawTextBiDiModeFlags(Flags);
  //Canvas.Font := Font;

  FillChar(lf, SizeOf(lf), Byte(0));
  lf.lfHeight := -MulDiv(FFontSize, FScreenLogPixels, 72);
  lf.lfWidth := 0;
  lf.lfWeight := FFontWeightValue;
  lf.lfQuality := ANTIALIASED_QUALITY;
  lf.lfCharSet := HANGEUL_CHARSET;
  if FFontStrike then
     lf.lfStrikeOut := 1
  else
     lf.lfStrikeOut := 0;
  StrCopy(lf.lfFaceName, PChar(FFontFace));
  Canvas.Font.Handle := CreateFontIndirect(lf);

  If AutoResizeText Then
  Begin
     TextBaseSize := Canvas.TextExtent(Caption);
     If TextBaseSize.cx >= Width Then
     Begin
        ResizeCandidate := FFontSize;

        For AutoResizeLoop := 1 to 4 do
        Begin
           ResizeCandidate := ResizeCandidate - 1;
           If ResizeCandidate < 1 Then Break; // 1º¸´Ù Àı´ë ÀÛÀ»¼ö ¾øÀ½.

           lf.lfHeight := -MulDiv(ResizeCandidate, FScreenLogPixels, 72);
           Canvas.Font.Handle := CreateFontIndirect(lf);

           TextBaseSize := Canvas.TextExtent(Caption);
           If TextBaseSize.cx < Width Then Break;
        End;
     End;
  End;

  Canvas.Font.Color := FFontColor;

  if not Enabled then
  begin
    OffsetRect(Rect, 1, 1);
    Canvas.Font.Color := clBtnHighlight;
    DrawText(Canvas.Handle, PChar(Text), Length(Text), Rect, Flags);
    OffsetRect(Rect, -1, -1);
    Canvas.Font.Color := clBtnShadow;
    DrawText(Canvas.Handle, PChar(Text), Length(Text), Rect, Flags);
  end
  else
    DrawText(Canvas.Handle, PChar(Text), Length(Text), Rect, Flags);
end;

procedure TJkhLabel.SetAutoResizeText(const Value: Boolean);
begin
  FAutoResizeText := Value;
  Invalidate;
end;

procedure TJkhLabel.SetFixedDPI(const Value: Integer);
var
  DC: HDC;
begin
  FFixedDPI := Value;
  If FFixedDPI > 0 Then
     FScreenLogPixels := FFixedDPI
  Else
  Begin
     DC := GetDC(0);
     FScreenLogPixels := GetDeviceCaps(DC, LOGPIXELSY);
     ReleaseDC(0,DC);
  End;

  Invalidate;
end;

procedure TJkhLabel.SetFontColor(const Value: TColor);
begin
  FFontColor := Value;
  Invalidate;
end;

procedure TJkhLabel.SetFontFace(const Value: String);
begin
  FFontFace := Value;
  AdjustBounds;
end;

procedure TJkhLabel.SetFontSize(const Value: Integer);
begin
  FFontSize := Value;
  AdjustBounds;
end;

procedure TJkhLabel.SetFontStrike(const Value: Boolean);
begin
  FFontStrike := Value;
  AdjustBounds;
end;

procedure TJkhLabel.SetFontWeight(const Value: TJkhFontWeight);
begin
  If Value = FFontWeight Then Exit;

  FFontWeight := Value;
  Case Value of
     fwDONTCARE:   FFontWeightValue := FW_DONTCARE;
     fwTHIN:       FFontWeightValue := FW_THIN;
     fwEXTRALIGHT: FFontWeightValue := FW_EXTRALIGHT;
     fwLIGHT:      FFontWeightValue := FW_LIGHT;
     fwNORMAL:     FFontWeightValue := FW_NORMAL;
     fwMEDIUM:     FFontWeightValue := FW_MEDIUM;
     fwSEMIBOLD:   FFontWeightValue := FW_SEMIBOLD;
     fwBOLD:       FFontWeightValue := FW_BOLD;
     fwEXTRABOLD:  FFontWeightValue := FW_EXTRABOLD;
     fwHEAVY:      FFontWeightValue := FW_HEAVY;
  End;

  AdjustBounds;
  Repaint;
end;

end.
